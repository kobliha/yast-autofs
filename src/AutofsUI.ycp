/**
 * File:        modules/AutofsUI.ycp
 * Package:     Configuration of autofs
 * Summary:     UI-related routines to be run from perl modules (Autofs.pm etc.)
 * Author:      Peter Varkoly <varkoly@novell.com>
 *
 * $Id: AutofsUI.ycp 26218 2005-11-21 15:15:22Z jsuchome $
 */
{
    module "AutofsUI";
    textdomain "autofs";
    import "Label";
    import "Popup";

    include "autofs/helps.ycp";

    global define string GetLDAPServer(string hostname ) ``{
        UI::OpenDialog(`opt(`decorated),
                       `HBox(`HSpacing(1),
                             `VBox(`VSpacing(0.2),
                                   `HSpacing (30),
				   `RichText(`id(`HelpText), HELPS["GetLDAPServer"]:""),
                                   // text entry label
                                   `TextEntry(`id(`ldapserver), _("DNS Name or IP-address of the LDAP-server"), hostname),
                                   `VSpacing(0.2),
                                   // ok pushbutton: confirm the dialog
                                   `HBox(`PushButton(`id(`ok),     Label::OKButton()),
                                         `PushButton(`id(`cancel), Label::CancelButton())),
                                   `VSpacing(0.2)),
                             `HSpacing(1)
                           ));

        any        ret = nil;
        string ldapserver = "no";
        while(true) {
            ret = UI::UserInput();
            /* abort? */
            if(ret == `abort || ret == `cancel) {
                if(Popup::ReallyAbort(false)) break;
                else continue;
            }
            else if(ret == `ok) {
                ldapserver = (string) UI::QueryWidget(`id(`ldapserver), `Value);
                break;
            }
            else {
                y2error("unexpected retcode: %1", ret);
                continue;
            }
        }
        UI::CloseDialog ();
	return ldapserver;
    }

}
